plugins {
    id 'java'
    id 'war'
    id("org.springframework.boot") version "2.4.4"
    id("io.spring.dependency-management") version "1.0.11.RELEASE"
}

group 'ru.ifmo'
version '0.2'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
bootWar {
    destinationDirectory = file("./env/war")
    archiveName("worker.war")
}
war {
    destinationDirectory = file("./env/war")
    archiveName("worker.war")
}

task deploy(type: Exec, dependsOn: bootWar) {
    commandLine "docker-compose",
            "-f", "env/docker-compose.yml",
            "up", "--build", "--force-recreate", "--no-deps",
            "-d", "application"
}

task helios(type: Exec, dependsOn: war) {
    commandLine "./env/helios.sh"
}

test {
    useJUnitPlatform()
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    junitVersion = '5.8.2'
    lombokVersion = '1.18.22'
}

configurations.implementation {
    exclude module: "spring-boot-starter-logging"
    exclude module: "logback-classic"
}

dependencies {
    // Worker
    implementation(project(":shared"))

    // Lombok
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    compileOnly("org.projectlombok:lombok:${lombokVersion}")

    //Spring
    implementation("org.springframework.boot:spring-boot-starter-web")
    developmentOnly("org.springframework.boot:spring-boot-devtools")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
    providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
    providedRuntime("org.springframework.boot:spring-boot-starter-logging")

    // XML
    implementation('com.sun.xml.bind:jaxb-core:3.0.1')
    implementation('javax.xml.bind:jaxb-api:2.4.0-b180830.0359')
    implementation('com.sun.xml.bind:jaxb-impl:3.0.1')
    implementation('org.javassist:javassist:3.28.0-GA')

    // Database
    implementation('commons-dbutils:commons-dbutils:1.7')
    implementation('org.postgresql:postgresql:42.3.1')
    implementation('ca.krasnay:sqlbuilder:1.3')
}